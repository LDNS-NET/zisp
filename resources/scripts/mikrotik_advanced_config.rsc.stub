# ZiSP MikroTik Advanced Configuration Script
# Generated for router: {{name}}
# Router ID: {{router_id}}

:put "==================== ZiSP ADVANCED CONFIGURATION ===================="

# Configuration variables
:local radiusip "{{radius_ip}}"
:local radiussecret "{{radius_secret}}"
:local bridgeName "zisp_bridge"
:local poolName "zisp_pool"
:local hotspotProfile "zisp_hs_prof"
:local pppoeProfile "Z_INTERNET"
:local snmpCommunity "{{snmp_community}}"
:local snmpLocation "{{snmp_location}}"
:local apiPort "{{api_port}}"
:local apiUser "{{username}}"
:local apiPassword "{{router_password}}"
:local trustedIp "{{trusted_ip}}"
:local hotspotUrl "{{hotspot_url}}"


:put " =================== Starting advanced configuration =================== "

:put " =================== CLEANUP PHASE =================== "
# Remove resources in reverse dependency order to avoid "in use" errors

# 1. Remove Hotspot Server & Profile
:put "Cleaning up Hotspot..."
:do { /ip hotspot remove [find name=$bridgeName] } on-error={}
:do { /ip hotspot remove [find interface=$bridgeName] } on-error={}
:do { /ip hotspot profile remove [find name=$hotspotProfile] } on-error={}
:do { /ip hotspot user profile remove [find name="default"] } on-error={} 

# 2. Remove DHCP Server & Network
:put "Cleaning up DHCP..."
:do { /ip dhcp-server remove [find name="zisp_dhcp"] } on-error={}
:do { /ip dhcp-server remove [find interface=$bridgeName] } on-error={}
:do { /ip dhcp-server network remove [find comment="zisp_hs_network"] } on-error={}
:do { /ip dhcp-server network remove [find address="172.20.0.0/16"] } on-error={}

# 3. Remove PPPoE Server & Profile
:put "Cleaning up PPPoE..."
:do { /interface pppoe-server server remove [find service-name="zisp_pppoe"] } on-error={}
:do { /interface pppoe-server server remove [find interface=$bridgeName] } on-error={}
:do { /ppp profile remove [find name=$pppoeProfile] } on-error={}

# 4. Remove IP Pool (used by DHCP, Hotspot, PPPoE)
:put "Cleaning up IP Pool..."
:do { /ip pool remove [find name=$poolName] } on-error={}

# 5. Remove IP Address
:put "Cleaning up IP Address..."
:do { /ip address remove [find interface=$bridgeName] } on-error={}

# 6. Remove Bridge
:put "Cleaning up Bridge..."
:do { /interface bridge remove [find name=$bridgeName] } on-error={}


:put " =================== CREATION PHASE =================== "

:put " =================== Creating bridge =================== "
/interface bridge add name=$bridgeName
:put " =================== Bridge created =================== "


:put " =================== Configuring bridge address =================== "
/ip address add address=172.20.0.1/16 interface=$bridgeName
:put " =================== Bridge address configured =================== "


:put " =================== Creating unified IP pool for hotspot and PPPoE =================== "
/ip pool add name=$poolName ranges=172.20.0.2-172.20.255.254
:put " =================== Unified IP pool created =================== "


:put " =================== Creating DHCP server =================== "
/ip dhcp-server add address-pool=$poolName disabled=no interface=$bridgeName lease-time=00:10:00 name=zisp_dhcp
:put " =================== DHCP server created =================== "


:put " =================== Configuring DHCP network =================== "
/ip dhcp-server network add address=172.20.0.0/16 comment="zisp_hs_network" gateway=172.20.0.1 dns-server=1.1.1.1,8.8.8.8
:put " =================== DHCP network configured =================== "


:put " =================== Creating hotspot profile =================== "
/ip hotspot profile add hotspot-address=172.20.0.1 dns-name=hotspot.zisp.cloud login-by=cookie,http-chap,http-pap,mac-cookie name=$hotspotProfile use-radius=yes radius-interim-update=00:10:00
:put " =================== Hotspot profile created =================== "


:put " =================== Creating hotspot server =================== "
/ip hotspot add address-pool=$poolName addresses-per-mac=1 disabled=no idle-timeout=30m keepalive-timeout=10m interface=$bridgeName name=$bridgeName profile=$hotspotProfile
:put " =================== Hotspot server created =================== "



:put " =================== Downloading and configuring hotspot HTML templates =================== "
# Download hotspot template files from server
:local baseUrl "{{hotspot_url}}"
:local templateFiles [:toarray "login.html,alogin.html,rlogin.html,flogin.html,logout.html,redirect.html,error.html"]

:put ("Base URL: " . $baseUrl)

# Create zisp-hotspot directory if it doesn't exist
:do {
    /file remove [find name="zisp-hotspot"]
} on-error={}

:foreach fileName in=$templateFiles do={
    :local downloadUrl ($baseUrl . "/hotspot-templates/" . $fileName)
    :put ("Downloading from: " . $downloadUrl)
    :do {
        /tool fetch url=$downloadUrl mode=https dst-path=("zisp-hotspot/" . $fileName)
        :delay 2s
        # Check if file was downloaded
        :local fileExists [/file find name=("zisp-hotspot/" . $fileName)]
        :if ([:len $fileExists] > 0) do={
            :put ("  SUCCESS: " . $fileName . " downloaded")
        } else={
            :put ("  WARNING: " . $fileName . " not found after download")
        }
    } on-error={
        :put ("  FAILED: Could not download " . $fileName)
        :put ("  Check: 1) URL is accessible, 2) HTTPS certificate is valid, 3) DNS resolution works")
    }
}
:put " =================== Hotspot HTML templates configured =================== "




:put " =================== Configuring hotspot masquerade =================== "
:do {
    /ip firewall nat remove [find comment="masquerade hotspot network"]
} on-error={}
/ip firewall nat add action=masquerade chain=srcnat comment="masquerade hotspot network" src-address=172.20.0.0/16
:put " =================== Hotspot masquerade configured =================== "


:put " =================== Disabling fasttrack =================== "
:do {
    /ip firewall filter disable [find where comment="defconf: fasttrack"]
    :put "Fasttrack disabled"
} on-error={
    :put " =================== No fasttrack rules found =================== "
}

# ============================================================================




:put " =================== Creating PPPoE profile =================== "
/ppp profile add dns-server=8.8.8.8,8.8.4.4 local-address=$poolName name=$pppoeProfile remote-address=$poolName
:put " =================== PPPoE profile created =================== "


:put " =================== Creating PPPoE server =================== "
/interface pppoe-server server add authentication=pap,chap default-profile=$pppoeProfile disabled=no interface=$bridgeName one-session-per-host=yes keepalive-timeout=10 service-name="zisp_pppoe"
:put " =================== PPPoE server created =================== "


:put " =================== Configuring RADIUS authentication =================== "
# Remove existing RADIUS client with same IP
:do { /radius remove [find address=$radiusip] } on-error={}
# Add (or re-add) RADIUS client for PPP + Hotspot
:do {
    /radius add service=ppp,hotspot address=$radiusip secret=$radiussecret disabled=no timeout=3000ms accounting-port=1813 authentication-port=1812
} on-error={:put "Warning: could not add RADIUS client"}
# Enable PPP AAA to use RADIUS
/ppp aaa set use-radius=yes accounting=yes interim-update=1h
# Global DNS settings for speed
/ip dns set allow-remote-requests=yes servers=1.1.1.1,8.8.8.8 cache-size=2048KiB cache-max-ttl=2h
:put " =================== RADIUS and DNS configured =================== "


:put " =================== Configuring PPPoE masquerade =================== "
:do {
    /ip firewall nat remove [find comment="masquerade pppoe network"]
} on-error={}
/ip firewall nat add action=masquerade chain=srcnat comment="masquerade pppoe network" out-interface=ether1
:put " =================== PPPoE masquerade configured =================== "


:put " =================== Mangle rules skipped for better mobile compatibility =================== "


:put " =================== Configuring SNMP =================== "
:do {
    :local existingCommunities [/snmp community find name=$snmpCommunity]
    :if ([:len $existingCommunities] > 0) do={
        /snmp community remove [find name=$snmpCommunity]
        :put " =================== Previous SNMP community with the same name removed =================== "
    }
} on-error={}
/snmp community add name=$snmpCommunity addresses=207.154.204.144 read-access=yes write-access=yes comment="ZiSP SNMP Community"
/snmp set enabled=yes location=$snmpLocation contact="ZiSP Management"
:put " =================== SNMP configured =================== "


:put " =================== Configuring Walled Garden (Minimum Required) =================== "
:do {
    /ip hotspot walled-garden remove [find comment="zisp-walled-garden"]
} on-error={}
/ip hotspot walled-garden add action=allow dst-host=*.bunny.net comment="zisp-walled-garden"
/ip hotspot walled-garden add action=allow dst-host=*.mikrotik.com comment="zisp-walled-garden"
/ip hotspot walled-garden add action=allow dst-host=connectivitycheck.gstatic.com comment="zisp-walled-garden" 
/ip hotspot walled-garden add action=allow dst-host=connectivitycheck.android.com comment="zisp-walled-garden"
/ip hotspot walled-garden add action=allow dst-host=*.paystack.com comment="zisp-walled-garden"
/ip hotspot walled-garden add action=allow dst-host=*.paystack.co comment="zisp-walled-garden"
/ip hotspot walled-garden add action=allow dst-host=paystack.com comment="zisp-walled-garden"
/ip hotspot walled-garden add action=allow dst-host=paystack.co comment="zisp-walled-garden"

# Allow tenant domain
:local tenantDomain "{{tenant_domain}}"
:if ([:len $tenantDomain] > 0) do={
    /ip hotspot walled-garden add action=allow dst-host=$tenantDomain comment="zisp-walled-garden-tenant"
    /ip hotspot walled-garden add action=allow dst-host=("*." . $tenantDomain) comment="zisp-walled-garden-tenant"
    :put ("Added walled garden for tenant: " . $tenantDomain)
}
:put " =================== Walled garden configured =================== "


:put "<<<<======= CONFIGURATION COMPLETE =============>>>>>"
:put "Bridge: $bridgeName"
:put "Hotspot: Enabled on bridge"
:put "PPPoE: Enabled on bridge"
:put "RADIUS: Enabled"
:put "SNMP: Enabled"
:put "Reboot: 3AM daily"
:put "=============================================================="
