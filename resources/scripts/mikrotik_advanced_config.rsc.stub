# ZiSP MikroTik Advanced Configuration Script
# Generated for router: {{name}}
# Router ID: {{router_id}}

:put "==================== ZiSP ADVANCED CONFIGURATION ===================="

# Configuration variables
:local radiusip "{{radius_ip}}"
:local radiussecret "{{radius_secret}}"
:local bridgeName "zisp_bridge"
:local poolName "zisp_pool"
:local hotspotProfile "zisp_hs_prof"
:local pppoeProfile "Z_INTERNET"
:local snmpCommunity "{{snmp_community}}"
:local snmpLocation "{{snmp_location}}"
:local apiPort "{{api_port}}"
:local apiUser "{{username}}"
:local apiPassword "{{router_password}}"
:local trustedIp "{{trusted_ip}}"
:local hotspotUrl "{{hotspot_url}}"

:put " =================== Starting advanced configuration =================== "


:put " =================== Creating bridge =================== "
:do {
    /interface bridge remove [find name=$bridgeName]
} on-error={}
/interface bridge add name=$bridgeName
:put " =================== Bridge created =================== "


:put " =================== Configuring bridge address =================== "
:do {
    /ip address remove [find interface=$bridgeName]
} on-error={}
/ip address add address=172.20.0.1/16 interface=$bridgeName
:put " =================== Bridge address configured =================== "


:put " =================== Creating unified IP pool for hotspot and PPPoE =================== "
:do {
    /ip pool remove [find name=$poolName]
} on-error={}
/ip pool add name=$poolName ranges=172.20.0.2-172.20.255.254
:put " =================== Unified IP pool created =================== "


:put " =================== Creating DHCP server =================== "
:do {
    /ip dhcp-server remove [find interface=$bridgeName]
} on-error={}
/ip dhcp-server add address-pool=$poolName disabled=no interface=$bridgeName lease-time=00:10:00 name=zisp_dhcp
:put " =================== DHCP server created =================== "


:put " =================== Configuring DHCP network =================== "
:do {
    /ip dhcp-server network remove [find address=172.20.0.0/16]
} on-error={}
/ip dhcp-server network add address=172.20.0.0/16 comment="zisp_hs_network" gateway=172.20.0.1
:put " =================== DHCP network configured =================== "


:put " =================== Creating hotspot profile =================== "
:do {
    /ip hotspot profile remove [find name=$hotspotProfile]
} on-error={}
/ip hotspot profile add hotspot-address=172.20.0.1 login-by=cookie,http-chap,http-pap,mac-cookie name=$hotspotProfile use-radius=yes radius-interim-update=00:10:00
:put " =================== Hotspot profile created =================== "


:put " =================== Creating hotspot server =================== "
:do {
    /ip hotspot remove [find interface=$bridgeName]
} on-error={}
/ip hotspot add address-pool=$poolName addresses-per-mac=1 disabled=no idle-timeout=1m interface=$bridgeName name=$bridgeName profile=$hotspotProfile
:put " =================== Hotspot server created =================== "


:put " =================== Downloading and configuring hotspot HTML templates =================== "
# Download hotspot template files from server
:local baseUrl "{{hotspot_url}}"
:local templateFiles [:toarray "login.html,alogin.html,rlogin.html,flogin.html,logout.html,redirect.html,error.html"]

:foreach fileName in=$templateFiles do={
    :local downloadUrl ($baseUrl . "/hotspot-templates/" . $fileName)
    :put ("Downloading: " . $fileName)
    :do {
        /tool fetch url=$downloadUrl mode=https dst-path=("hotspot/" . $fileName)
        :put ("  Success: " . $fileName)
    } on-error={
        :put ("  Failed to download: " . $fileName)
    }
    :delay 1s
}
:put " =================== Hotspot HTML templates configured =================== "


:put " =================== Configuring hotspot masquerade =================== "
:do {
    /ip firewall nat remove [find comment="masquerade hotspot network"]
} on-error={}
/ip firewall nat add action=masquerade chain=srcnat comment="masquerade hotspot network" src-address=172.20.0.0/16
:put " =================== Hotspot masquerade configured =================== "


:put " =================== Disabling fasttrack =================== "
:do {
    /ip firewall filter disable [find where comment="defconf: fasttrack"]
    :put "Fasttrack disabled"
} on-error={
    :put " =================== No fasttrack rules found =================== "
}

# ============================================================================




:put " =================== Creating PPPoE profile =================== "
:do {
    /ppp profile remove [find name=$pppoeProfile]
} on-error={}
/ppp profile add dns-server=8.8.8.8,8.8.4.4 local-address=$poolName name=$pppoeProfile remote-address=$poolName
:put " =================== PPPoE profile created =================== "


:put " =================== Creating PPPoE server =================== "
:do {
    /interface pppoe-server server remove [find interface=$bridgeName]
} on-error={}
/interface pppoe-server server add authentication=pap,chap default-profile=$pppoeProfile disabled=no interface=$bridgeName one-session-per-host=yes keepalive-timeout=10 service-name="zisp_pppoe"
:put " =================== PPPoE server created =================== "


:put " =================== Configuring RADIUS authentication =================== "
# Remove existing RADIUS client with same IP
:do { /radius remove [find address=$radiusip] } on-error={}
# Add (or re-add) RADIUS client for PPP + Hotspot
:do {
    /radius add service=ppp,hotspot address=$radiusip secret=$radiussecret disabled=no timeout=3000ms accounting-port=1813 authentication-port=1812
} on-error={:put "Warning: could not add RADIUS client"}
# Enable PPP AAA to use RADIUS
/ppp aaa set use-radius=yes accounting=yes interim-update=1h
:put " =================== RADIUS authentication enabled =================== "


:put " =================== Configuring PPPoE masquerade =================== "
:do {
    /ip firewall nat remove [find comment="masquerade pppoe network"]
} on-error={}
/ip firewall nat add action=masquerade chain=srcnat comment="masquerade pppoe network" out-interface=ether1
:put " =================== PPPoE masquerade configured =================== "


:put " =================== Configuring antisharing and router IP hiding =================== "
:do {
    /ip firewall mangle remove [find comment="anti-sharing-connection"]
} on-error={}
/ip firewall mangle add action=change-ttl chain=postrouting comment="anti-sharing-connection" new-ttl=set:1 out-interface=$bridgeName passthrough=yes

:do {
    /ip firewall mangle remove [find comment="hide-router-ip-address"]
} on-error={}
/ip firewall mangle add action=change-ttl chain=prerouting comment="hide-router-ip-address" new-ttl=increment:2 passthrough=yes
:put " =================== Mangle rules configured =================== "


:put " =================== Configuring SNMP =================== "
:do {
    :local existingCommunities [/snmp community find name=$snmpCommunity]
    :if ([:len $existingCommunities] > 0) do={
        /snmp community remove [find name=$snmpCommunity]
        :put " =================== Previous SNMP community with the same name removed =================== "
    }
} on-error={}
/snmp community add name=$snmpCommunity addresses=207.154.204.144 read-access=yes write-access=yes comment="ZiSP SNMP Community"
/snmp set enabled=yes location=$snmpLocation contact="ZiSP Management"
:put " =================== SNMP configured =================== "


:put " =================== Configuring walled garden =================== "
:do {
    /ip hotspot walled-garden remove [find comment="zisp-walled-garden"]
} on-error={}
/ip hotspot walled-garden add action=allow dst-host=*.google.com comment="zisp-walled-garden"
/ip hotspot walled-garden add action=allow dst-host=*.googleapis.com comment="zisp-walled-garden"
/ip hotspot walled-garden add action=allow dst-host=*.gstatic.com comment="zisp-walled-garden"
/ip hotspot walled-garden add action=allow dst-host=*.mikrotik.com comment="zisp-walled-garden"
:put " =================== Walled garden configured =================== "


:put "<<<<======= CONFIGURATION COMPLETE =============>>>>>"
:put "Bridge: $bridgeName"
:put "Hotspot: Enabled on bridge"
:put "PPPoE: Enabled on bridge"
:put "RADIUS: Enabled"
:put "SNMP: Enabled"
:put "Reboot: 3AM daily"
:put "=============================================================="
